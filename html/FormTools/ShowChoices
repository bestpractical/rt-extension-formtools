% my $queue = $m->notes('queue');
% my $pages = $DECODED_ARGS->{'form_config'}{'formtools-pages'};
% for my $page ( sort { $pages->{$a}{'sort_order'} <=> $pages->{$b}{'sort_order'} } grep { exists $pages->{$_}{'sort_order'} } keys %$pages ) {
<%perl>
    my @fields;
    my @elements = @{ $pages->{$page}{'content'} };
    my %field_value;
    for my $element (@elements) {
        my ( $input_name, $is_html );
        my $name;
        if ( $name = $element->{'input-name'} ) {
            next if ( $element->{'type'} // '' ) eq 'hidden';
            $input_name = $name;
        }
        elsif ( $name = $element->{'arguments'}{'name'} ) {
            next if ( $element->{'arguments'}{'render_as'} // '' ) eq 'hidden';
            if ( RT::Extension::FormTools::is_core_field($name) ) {
                $input_name = $name;
            }
            else {
                my $cf = RT::CustomField->new( $session{'CurrentUser'} );
                $cf->LoadByName( Name => $name, Queue => $queue->Id );
                $cf->LoadByName( Name => $name, Queue => 0 ) unless $cf->Id;
                $cf->LoadByName( Name => $name, LookupType => RT::Ticket->CustomFieldLookupType ) unless $cf->Id;
                if ( $cf->Id ) {
                    $input_name = GetCustomFieldInputName(
                        Object      => RT::Ticket->new( $session{CurrentUser} ),
                        CustomField => $cf,
                    );
                    $is_html = $cf->Type eq 'HTML';
                }
            }
        }

        if ($input_name) {

            my $args = $element->{arguments} || {};
            if ( $args->{show_condition} && $args->{show_condition}{enabled} && $args->{show_condition}{name} ) {
                my $value = $field_value{$args->{show_condition}{name}} // '';
                my $matched;
                for my $val ( ref $value ? @$value : $value ) {
                    if ( grep { $val eq $_ } @{$args->{show_condition}{values} || []} ) {
                        $matched = 1;
                        last;
                    }
                }
                next unless $matched;
            }

            if ( $input_name eq 'Attach' ) {
                push @fields, { name => $name, value => $ARGS{AttachedFilenames} };
            }
            elsif ( ( ( $input_name eq 'Content' ) || $is_html ) && RT->Config->Get( 'MessageBoxRichText',  $session{'CurrentUser'} ) ) {
                push @fields, { name => $name, raw_value => $DECODED_ARGS->{$input_name} };
            }
            else {
                push @fields, { name => $name, value => $DECODED_ARGS->{$input_name} };
            }

            $field_value{$name} = $fields[-1]{value} || $fields[-1]{raw_value};
        }
    }
</%perl>

% if( @fields ) {
<h4><% $pages->{ $page }{'name'} %></h4>
<div>
% foreach my $field (@fields) {
<& /Elements/LabeledValue, Label => loc($field->{name}), ValueSpanClass => 'current-value', RawValue => $field->{raw_value}, Value => ref $field->{value} ? join(', ', @{$field->{value}}) : $field->{value} &>
% }
</div><br />
% }
% }

